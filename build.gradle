plugins {
    id 'java'
}

group = 'me.figsq.pretendpoke.pretendpoke.Main'
version = '1.4.6-BETA'

subprojects {
    apply plugin: 'java'

    repositories {
        mavenLocal()
        mavenCentral()
        maven {
            name = 'spigotmc-repo'
            url = 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/'
        }
        maven {
            allowInsecureProtocol = true
            url "http://server.pokemtd.top:31647/snapshots"
        }
        maven {
            url = 'https://repo.minebench.de/'
        }
    }

    dependencies {
        compileOnly 'me.clip:placeholderapi:2.10.3'

        compileOnly 'me.fullidle.ficore:FICore:1.2.6'

        testImplementation 'org.junit.jupiter:junit-jupiter-api:5.9.1'
        testImplementation 'org.junit.jupiter:junit-jupiter-params:5.9.1'
        testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.9.1'
        compileOnly 'org.projectlombok:lombok:1.18.28'
        annotationProcessor 'org.projectlombok:lombok:1.18.28'
    }
    if (name != 'common') {
        var commonProject = project(':common') as Project

        dependencies {
            implementation commonProject
        }

        jar {
            from(commonProject.sourceSets.main.output)
        }
    }

    java {
        sourceCompatibility = targetCompatibility = 1.8
    }

    test {
        useJUnitPlatform()
    }

    processResources {
        filteringCharset 'UTF-8'
        filesMatching('plugin.yml') {
            expand project.properties
        }
    }

    jar {
        archiveBaseName = "${rootProject.name}-${project.name}"
        version = rootProject.version
    }
}